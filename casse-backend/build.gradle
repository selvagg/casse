plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.cloud.tools.jib' version '3.4.0'
}

group = 'casse.music'
version = 'v0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.liquibase:liquibase-core:4.17.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.session:spring-session-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'software.amazon.awssdk:s3:2.20.120'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.postgresql:postgresql:42.5.1'
    implementation 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
    useJUnitPlatform()
}

// Get version from project or fallback
def appVersion = project.version ?: "latest"

// Get short Git commit hash (first 5 characters)
def gitCommitShort = "git rev-parse --short=7 HEAD".execute().text.trim()

// Build the final image tag
def branchName = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
def branchPrefix = branchName.split('/')[0]
def imageTag = "${appVersion}-${branchPrefix}-${gitCommitShort}"

jib {
    from {
        image = 'eclipse-temurin:21-jdk'
    }
    to {
        image = "starflutesg/casse-backend:${imageTag}"
        auth {
            username = System.getenv("DOCKER_HUB_USERNAME")
            password = System.getenv("DOCKER_HUB_PASSWORD")
        }
    }
    container {
        jvmFlags = ['-Xms512m', '-Xmx1024m']
        ports = ['8080']
    }
}
